grammar com.veapps.dnn.Dls with org.eclipse.xtext.common.Terminals

generate dls "http://www.veapps.com/dnn/Dls"

Model:
	network=Network;

Network:
	'network' name=STRING '{' &
		'epochs' ASSIGN epochs=INTEGER &
		'crop-size' ASSIGN cropSize=INTEGER &
		'batch-size' ASSIGN batchSize=INTEGER &
		('seed' ASSIGN seed=ZINTEGER)? &
		'updater' REFERENCE updaterType=UpdaterType &
		'learningRate' ASSIGN learningRate=REAL
		layers+=Layer*
	'}'
;

Layer:
	'conv' layerDecl=LayerDeclaration '{' convLayerBody=ConvLayerBody layerBody=LayerBody '}'
	| 'pool' layerDecl=LayerTuple '{' poolLayerBody=PoolLayerBody layerBody=LayerBody '}'
	| 'dense' layerDecl=LayerDeclaration ('{' layerBody=LayerBody '}')?
	| 'softmax' layerDecl=LayerTuple ('{' layerBody=LayerBody '}')?
;

LayerBody:
	('activation' REFERENCE activType=ActivationFunctionType)? &
	('weightsInit' REFERENCE weightInit=WeightInitType)? &
	('biasInit' REFERENCE biasInit=WeightInitType)? &
	(SEPARATOR)*
;

ConvLayerBody:
	'kernel-size' ASSIGN kernelSize=INTEGER &
	'stride' ASSIGN stride=INTEGER &
	(SEPARATOR)*
;

PoolLayerBody:
	'type' REFERENCE poolingType=PoolingType &
	'stride' ASSIGN stride=INTEGER &
	(SEPARATOR)*
;

LayerDeclaration:
	layerTuple+=LayerTuple 
	| '[' layerTuple+=LayerTuple (SEPARATOR layerTuple+=LayerTuple)* ']'
;

LayerTuple:
	'(' layerName=LayerName (in=InParam)? out=OutParam ')'
;

LayerName:
	'name' ':' name=STRING
;

InParam:
	'in' ':' name=STRING
;

OutParam:
	'out' ':' val=INTEGER
;

enum PoolingType:
	MAX_POOLING='max' | AVERAGE_POOLING='avg'
;

enum ActivationFunctionType:
	SIGMOID='sigmoid' | TANH='tanh' | RELU='relu'
;

enum UpdaterType:
	SGD='sgd' | ADAM='adam' | ADADELTA='adadelta' | NESTEROVS='nestrovs' | ADAGRAD='adagrad' | RMSPROP='rmsprop'
;

enum WeightInitType:
	ZERO='zero' | UNIFORM='uniform' | NORMALIZED='normalized' | XAVIER='xavier'
;

terminal EXT_INT:
	INT ('e' | 'E')('-' | '+') INTEGER
;

terminal INTEGER:
	('1'..'9')(INT)*
;

terminal ZINTEGER:
	INT
;

terminal REAL:
	INTEGER | INT '.' (EXT_INT | INT) (INT)*
;

terminal ASSIGN:
	'='
;

terminal SEPARATOR:
	','
;

terminal REFERENCE:
	'->'
;

terminal ML_COMMENT:
	'/*' -> '*/'
;
